{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 154,
   "id": "7e35f606",
   "metadata": {},
   "outputs": [],
   "source": [
    "using LinearAlgebra\n",
    "using TensorKit\n",
    "using TensorKitSectors\n",
    "using SymGroupRepresentations\n",
    "const SGR = SymGroupRepresentations;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 155,
   "id": "df8f59d5",
   "metadata": {},
   "outputs": [],
   "source": [
    "q = 5\n",
    "S = SNIrrep{q};\n",
    "irrep_gen = SGR.irreps_gen.S5;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 156,
   "id": "1995fa55",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "classical_potts (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "# usual Potts tensor\n",
    "function classical_potts(q::Int, β::Float64)\n",
    "    V = ℂ^q\n",
    "    A_potts = zeros(Float64, V ⊗ V ← V ⊗ V)\n",
    "    for (i, j, k, l) in Iterators.product(fill(1:q, 4)...)\n",
    "        E = -(Int(i == j) + Int(j == l) + Int(l == k) + Int(k == i))\n",
    "        A_potts[i, j, k, l] = exp(-β * E)\n",
    "    end\n",
    "    return A_potts\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 157,
   "id": "26781d06",
   "metadata": {},
   "outputs": [],
   "source": [
    "T0 = classical_potts(q, 0.5);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 158,
   "id": "c0e08e32",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "permrep_gen (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "# Permutation representation for generators \n",
    "# x1 = (1,2,...,n), x2 = (1,2)\n",
    "function permrep_gen(q::Int)\n",
    "    x1 = zeros(Float64, q, q)\n",
    "    x2 = zeros(Float64, q, q)\n",
    "    for i in 1:q\n",
    "        x1[mod1(i + 1, q), i] = 1\n",
    "    end\n",
    "    x2[1, 2] = x2[2, 1] = 1\n",
    "    for i in 3:q\n",
    "        x2[i, i] = 1\n",
    "    end\n",
    "    return [x1, x2]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 159,
   "id": "ec2f2170",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "5×5 Matrix{Float64}:\n",
       " 0.0  0.0  0.0  0.0  1.0\n",
       " 1.0  0.0  0.0  0.0  0.0\n",
       " 0.0  1.0  0.0  0.0  0.0\n",
       " 0.0  0.0  1.0  0.0  0.0\n",
       " 0.0  0.0  0.0  1.0  0.0"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    },
    {
     "data": {
      "text/plain": [
       "5×5 Matrix{Float64}:\n",
       " 0.0  1.0  0.0  0.0  0.0\n",
       " 1.0  0.0  0.0  0.0  0.0\n",
       " 0.0  0.0  1.0  0.0  0.0\n",
       " 0.0  0.0  0.0  1.0  0.0\n",
       " 0.0  0.0  0.0  0.0  1.0"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "permrep = permrep_gen(q);\n",
    "for rep in permrep\n",
    "    display(rep)\n",
    "    ρ = TensorMap(rep, ℂ^q ← ℂ^q)\n",
    "    @assert (ρ ⊗ ρ) * T0 * (ρ' ⊗ ρ') ≈ T0\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 160,
   "id": "515e38e5",
   "metadata": {},
   "outputs": [],
   "source": [
    "f = hcat(map(1:2) do c\n",
    "    SGR.get_intertwiners(irrep_gen[c], permrep)[1]\n",
    "end...)\n",
    "f = TensorMap(f, ℂ^q ← ℂ^q)\n",
    "T = (f' ⊗ f') * T0 * (f ⊗ f);\n",
    "T_arr = convert(Array, T);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 161,
   "id": "d6de2ee8",
   "metadata": {},
   "outputs": [],
   "source": [
    "for (rep1, rep2) in zip(irrep_gen[1], irrep_gen[2])\n",
    "    rep = Matrix(SGR.block_diag(rep1, rep2))\n",
    "    ρ = TensorMap(rep, ℂ^q ← ℂ^q)\n",
    "    @assert (ρ ⊗ ρ) * T * (ρ' ⊗ ρ') ≈ T\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 162,
   "id": "cf632b18",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "get_reduced_elements_potts (generic function with 1 method)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "function get_reduced_elements_potts(T_arr)\n",
    "    cs = [S([5]), S([4, 1])]\n",
    "    slices = [1:1, 2:5]\n",
    "    rdes = Vector{Pair{NTuple{q, S}, Float64}}()\n",
    "    for (i1, i2, i3, i4) in Iterators.product(fill(1:2, 4)...)\n",
    "        s1, s2, s3, s4 = cs[i1], cs[i2], cs[i3], cs[i4]\n",
    "        for s in intersect(s1 ⊗ s2, s3 ⊗ s4)\n",
    "            fin = fusiontensor(s3, s4, s)[:, :, :, 1]\n",
    "            fout = fusiontensor(s1, s2, s)[:, :, :, 1]\n",
    "            subT_arr = @view T_arr[slices[i1], slices[i2], slices[i3], slices[i4]]\n",
    "            @tensor reduced_matrix[-1; -2] := conj(fout[1 2; -1]) * subT_arr[1 2; 3 4] * fin[3 4; -2]\n",
    "            rde = reduced_matrix[1, 1]\n",
    "            @assert isapprox(reduced_matrix, rde * I; atol = 1.0e-10)\n",
    "            push!(rdes, (s1, s2, s3, s4, s) => rde)\n",
    "        end\n",
    "    end\n",
    "    return Dict(rdes)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 163,
   "id": "a7be2c31",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Dict{NTuple{5, S5Irrep}, Float64} with 15 entries:\n",
       "  (4₁1₁, 5₁, 4₁1₁, 4₁1₁, 4₁1₁)   => 1.16856\n",
       "  (5₁, 5₁, 5₁, 5₁, 5₁)           => 40.7533\n",
       "  (4₁1₁, 5₁, 5₁, 4₁1₁, 4₁1₁)     => 1.09551\n",
       "  (5₁, 4₁1₁, 4₁1₁, 5₁, 4₁1₁)     => 1.09551\n",
       "  (4₁1₁, 4₁1₁, 4₁1₁, 4₁1₁, 4₁1₁) => 1.18232\n",
       "  (4₁1₁, 4₁1₁, 4₁1₁, 4₁1₁, 3₁1₂) => 0.420839\n",
       "  (4₁1₁, 4₁1₁, 4₁1₁, 4₁1₁, 5₁)   => 3.1195\n",
       "  (5₁, 4₁1₁, 4₁1₁, 4₁1₁, 4₁1₁)   => 1.16856\n",
       "  (4₁1₁, 4₁1₁, 5₁, 5₁, 5₁)       => 9.5199\n",
       "  (5₁, 4₁1₁, 5₁, 4₁1₁, 4₁1₁)     => 4.75995\n",
       "  (4₁1₁, 4₁1₁, 5₁, 4₁1₁, 4₁1₁)   => 1.16856\n",
       "  (4₁1₁, 4₁1₁, 4₁1₁, 4₁1₁, 3₁2₁) => 0.420839\n",
       "  (5₁, 5₁, 4₁1₁, 4₁1₁, 5₁)       => 9.5199\n",
       "  (4₁1₁, 5₁, 4₁1₁, 5₁, 4₁1₁)     => 4.75995\n",
       "  (4₁1₁, 4₁1₁, 4₁1₁, 5₁, 4₁1₁)   => 1.16856"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "rdes = get_reduced_elements_potts(T_arr)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 167,
   "id": "7b550d82",
   "metadata": {},
   "outputs": [],
   "source": [
    "Vsym = Vect[S](S([5]) => 1, S([4, 1]) => 1)\n",
    "Tsym = zeros(Float64, Vsym ⊗ Vsym ← Vsym ⊗ Vsym);\n",
    "for (f1, f2) in fusiontrees(Tsym)\n",
    "    (s1, s2) = f1.uncoupled\n",
    "    (s3, s4) = f2.uncoupled\n",
    "    s = f1.coupled\n",
    "    Tsym[f1, f2] .= rdes[(s1, s2, s3, s4, s)]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 168,
   "id": "c1564444",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "true"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "convert(Array, Tsym) ≈ T_arr"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 166,
   "id": "6d287dd1",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
